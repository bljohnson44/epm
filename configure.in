dnl
dnl "$Id: configure.in,v 1.11 2000/05/01 14:25:11 mike Exp $"
dnl
dnl   Configuration script for the ESP Package Manager (EPM).
dnl
dnl   Copyright 1999-2000 by Easy Software Products, all rights reserved.
dnl
dnl   This program is free software; you can redistribute it and/or modify
dnl   it under the terms of the GNU General Public License as published by
dnl   the Free Software Foundation; either version 2, or (at your option)
dnl   any later version.
dnl
dnl   This program is distributed in the hope that it will be useful,
dnl   but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl   GNU General Public License for more details.
dnl

AC_INIT(epm.c)
AC_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr)

dnl Get the operating system...

uname=`uname`
if test "$uname" = "IRIX64"; then
    uname="IRIX"
fi

dnl Clear default debugging options and set normal optimization by
dnl default unless the user asks for debugging specifically.

CFLAGS="${CFLAGS:=}"
CXXFLAGS="${CXXFLAGS:=}"
LDFLAGS="${LDFLAGS:=}"
AC_SUBST(LDFLAGS)
OPTIM="-O"
AC_SUBST(OPTIM)

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]],
if eval "test x$enable_debug = xyes"; then
 	OPTIM="-g"
else
	LDFLAGS="$LDFLAGS -s"
fi)

dnl Checks for programs...
AC_PROG_CC
AC_PROG_CXX
AC_PATH_PROG(CHMOD,chmod)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(GZIP,gzip)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(STRIP,strip)
AC_PATH_PROG(WHOAMI,whoami)

dnl Architecture checks...
AC_C_CONST
AC_C_CHAR_UNSIGNED

if test "$uname" = "IRIX"; then
	if test `uname -r` = 6.5; then
		STRIP="$STRIP -f -s -k -l -h"
	fi
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT

dnl Checks for string functions.
AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(strcasecmp)
AC_CHECK_FUNCS(strncasecmp)

dnl Check for libraries...

AC_PATH_X
if test "$x_libraries" = "/usr/lib"; then
	echo "Ignoring X library directory \"$x_libraries\" requested by configure."
	x_libraries="NONE"
fi
if test "$x_libraries" != "NONE" -a "x$x_libraries" != "x"; then
	LDFLAGS="$LDFLAGS -L$x_libraries"
	if test "$uname" = "SunOS"; then
		LDFLAGS="$LDFLAGS -R$x_libraries"
	fi
fi
if test "$x_includes" = "/usr/include"; then
	echo "Ignoring X include directory \"$x_includes\" requested by configure."
	x_includes="NONE"
fi
if test "$x_includes" != "NONE" -a "x$x_includes" != "x"; then
	CFLAGS="$CFLAGS -I$x_includes"
	CXXFLAGS="$CXXFLAGS -I$x_includes"
fi
AC_CHECK_LIB(m,pow)
AC_CHECK_LIB(fltk,numericsort)

if test "$ac_cv_lib_fltk_numericsort" = yes -a "$ac_cv_prog_CXX" != ""; then
	SETUP="setup"
	INSTALL_SETUP="install-setup"
	LIBS="$LIBS -lXext -lX11"
else
	SETUP=""
	INSTALL_SETUP=""
	AC_MSG_WARN(C++ compiler or FLTK not available so setup GUI will not be built.)
fi

AC_CHECK_LIB(socket,socket,
if test "$uname" != "IRIX"; then
	LIBS="$LIBS -lsocket"
else
	echo "Not using -lsocket since you are running IRIX."
fi)
AC_CHECK_LIB(nsl,gethostbyaddr,
if test "$uname" != "IRIX"; then
	LIBS="$LIBS -lnsl"
else
	echo "Not using -lnsl since you are running IRIX."
fi)

AC_SUBST(SETUP)
AC_SUBST(INSTALL_SETUP)

dnl See if the echo command supports the \c escape; if not, then it's broken!
if test "x$ac_c" = "x"; then
	AC_DEFINE(HAVE_BROKEN_ECHO)
fi

dnl Update compiler options...
if test -n "$GCC"; then
	if test -z "$OPTIM"; then
		if test "$uname" = "HP-UX"; then
			OPTIM="-O1 -fforce-mem -fforce-addr -fcaller-saves"
		else
	        	OPTIM="-O2 -fforce-mem -fforce-addr -fcaller-saves"
		fi
	fi

	OPTIM="-Wall -Wno-return-type -fno-rtti $OPTIM"

	# See if GCC supports -fno-exceptions...
	echo "Testing if GCC supports -fno-exceptions..."
	OLDCFLAGS="$CFLAGS"
	CFLAGS="$CFLAGS -fno-exceptions"
	AC_TRY_COMPILE(,,
		OPTIM="$OPTIM -fno-exceptions"
		echo "Congratulations - your GCC supports exceptions; disabling them...",
		echo "GCC does not support exceptions...")
	CFLAGS="$OLDCFLAGS"

	# See if we are running Solaris; if so, try the -fpermissive option...
	if test "$uname" = SunOS; then
		echo "Testing if GCC supports -fpermissive..."

		OLDCFLAGS="$CFLAGS"
		CFLAGS="$CFLAGS -fpermissive"
		AC_TRY_COMPILE(,,
			OPTIM="$OPTIM -fpermissive"
			echo "Enabling -fpermissive to work around X header bugs...",
			echo "The -fpermissive option is not required by your GCC...")
		CFLAGS="$OLDCFLAGS"
	fi
fi

dnl Fix "prefix" variable if it hasn't been specified...
if test "$prefix" = "NONE"; then
	prefix="/usr"
fi

dnl EPM_SOFTWARE needs special attention for the default location...
if test "$prefix" = "/usr"; then
	EPM_SOFTWARE="/etc/software"
else
	EPM_SOFTWARE="$prefix/etc/software"
fi

AC_DEFINE_UNQUOTED(EPM_SOFTWARE, "$EPM_SOFTWARE")
AC_SUBST(EPM_SOFTWARE)

AC_DEFINE_UNQUOTED(EPM_GZIP, "$GZIP")
AC_SUBST(EPM_GZIP)

AC_DEFINE_UNQUOTED(EPM_STRIP, "$STRIP")
AC_SUBST(EPM_STRIP)

AC_DEFINE_UNQUOTED(EPM_BINDIR, "$prefix/bin")
AC_SUBST(EPM_BINDIR)

AC_DEFINE_UNQUOTED(EPM_LIBDIR, "$prefix/lib/epm")
AC_SUBST(EPM_LIBDIR)

AC_DEFINE_UNQUOTED(EPM_WHOAMI, "$WHOAMI")
AC_SUBST(EPM_WHOAMI)

AC_OUTPUT(Makefile)

dnl
dnl End of "$Id: configure.in,v 1.11 2000/05/01 14:25:11 mike Exp $".
dnl
