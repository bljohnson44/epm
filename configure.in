dnl
dnl "$Id: configure.in,v 1.44 2001/10/17 12:16:08 mike Exp $"
dnl
dnl   Configuration script for the ESP Package Manager (EPM).
dnl
dnl   Copyright 1999-2001 by Easy Software Products, all rights reserved.
dnl
dnl   This program is free software; you can redistribute it and/or modify
dnl   it under the terms of the GNU General Public License as published by
dnl   the Free Software Foundation; either version 2, or (at your option)
dnl   any later version.
dnl
dnl   This program is distributed in the hope that it will be useful,
dnl   but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl   GNU General Public License for more details.
dnl

AC_INIT(epm.c)
AC_CONFIG_HEADER(config.h)
AC_PREFIX_DEFAULT(/usr)

dnl Version number...

VERSION=3.2
VERNUMBER=320
AC_SUBST(VERSION)
AC_SUBST(VERNUMBER)
AC_DEFINE_UNQUOTED(EPM_VERSION, "ESP Package Manager v$VERSION")

dnl Get the operating system...

uname=`uname`
uversion=`uname -r | sed -e '1,$s/[[^0-9]]//g'`

if test "$uname" = "IRIX64"; then
    uname="IRIX"
fi

dnl Clear default debugging options and set normal optimization by
dnl default unless the user asks for debugging specifically.

CFLAGS="${CFLAGS:=}"
CXXFLAGS="${CXXFLAGS:=}"
LDFLAGS="${LDFLAGS:=}"
AC_SUBST(LDFLAGS)
OPTIM="-O"
AC_SUBST(OPTIM)

AC_ARG_ENABLE(debug, [  --enable-debug          turn on debugging [default=no]],
if eval "test x$enable_debug = xyes"; then
 	OPTIM="-g"
else
	LDFLAGS="$LDFLAGS -s"
fi)

AC_ARG_WITH(fltk-libs, [  --with-fltk-libs        set directory for FLTK library],
	LDFLAGS="-L$withval $LDFLAGS"
	fltklibs="$withval",
	fltklibs="")

AC_ARG_WITH(fltk-includes, [  --with-fltk-includes    set directory for FLTK includes],
	CFLAGS="-I$withval $CFLAGS"
	CXXFLAGS="-I$withval $CXXFLAGS",)

AC_ARG_WITH(docdir, [  --with-docdir           set directory for documentation],
	docdir="$withval",
	docdir="\${datadir}/doc/epm")

AC_SUBST(docdir)

AC_ARG_WITH(softwaredir, [  --with-softwaredir        set directory for EPM uninstall files and init scripts],
	softwaredir="$withval",
	softwaredir="")

dnl Checks for programs...
AC_PROG_CC
AC_PROG_CXX
AC_PATH_PROG(CHMOD,chmod)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(GZIP,gzip)
AC_PATH_PROG(HTMLDOC,htmldoc)
AC_PATH_PROG(MKDIR,mkdir)
AC_PATH_PROG(NROFF,nroff)
if test x$NROFF = x; then
    AC_PATH_PROG(GROFF,groff)
    if test x$GROFF = x; then
        NROFF="echo"
    else
        NROFF="$GROFF -T ascii"
    fi
fi
AC_PATH_PROG(PERL,perl)
AC_PATH_PROG(RM,rm)
AC_PATH_PROG(RPM,rpm)
AC_PATH_PROG(STRIP,strip)

dnl Architecture checks...
AC_C_CONST
AC_C_CHAR_UNSIGNED

if test "$uname" = "IRIX"; then
	if test `uname -r` = 6.5; then
		STRIP="$STRIP -f -s -k -l -h"
	fi
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_CHECK_HEADER(strings.h,AC_DEFINE(HAVE_STRINGS_H))
AC_CHECK_HEADER(sys/mount.h,AC_DEFINE(HAVE_SYS_MOUNT_H))
AC_CHECK_HEADER(sys/param.h,AC_DEFINE(HAVE_SYS_PARAM_H))
AC_CHECK_HEADER(sys/statfs.h,AC_DEFINE(HAVE_SYS_STATFS_H))
AC_CHECK_HEADER(sys/vfs.h,AC_DEFINE(HAVE_SYS_VFS_H))

dnl Checks for string functions.
AC_CHECK_FUNCS(strdup)
AC_CHECK_FUNCS(strcasecmp)
AC_CHECK_FUNCS(strncasecmp)
if test "$uname" = "HP-UX" -a "$uversion" = "1020"; then
	echo Forcing snprintf emulation for HP-UX.
else
	AC_CHECK_FUNCS(snprintf)
	AC_CHECK_FUNCS(vsnprintf)
fi

dnl Check for libraries...

AC_CHECK_LIB(m,pow)
AC_CHECK_LIB(socket,socket,
if test "$uname" != "IRIX"; then
	LIBS="$LIBS -lsocket"
else
	echo "Not using -lsocket since you are running IRIX."
fi)
AC_CHECK_LIB(nsl,gethostbyaddr,
if test "$uname" != "IRIX"; then
	LIBS="$LIBS -lnsl"
else
	echo "Not using -lnsl since you are running IRIX."
fi)

AC_PATH_X
if test "$x_libraries" = "/usr/lib"; then
	echo "Ignoring X library directory \"$x_libraries\" requested by configure."
	x_libraries="NONE"
fi
if test "$x_libraries" != "NONE" -a "x$x_libraries" != "x"; then
	LDFLAGS="$LDFLAGS -L$x_libraries"
	if test "$uname" = "SunOS"; then
		LDFLAGS="$LDFLAGS -R$x_libraries"
	fi
fi
if test "$x_includes" = "/usr/include"; then
	echo "Ignoring X include directory \"$x_includes\" requested by configure."
	x_includes="NONE"
fi
if test "$x_includes" != "NONE" -a "x$x_includes" != "x"; then
	CFLAGS="$CFLAGS -I$x_includes"
	CXXFLAGS="$CXXFLAGS -I$x_includes"
fi

dnl Try to find the static FLTK library...
AC_MSG_CHECKING(for static FLTK library)

if test x$fltklibs = x; then
	for dir in "" /usr /usr/openwin /usr/X11R6 /usr/local; do
		if test -f $dir/lib32/libfltk.a; then
			fltklibs=$dir/lib32
			if test x$dir != x/usr -a x$dir/include != x$x_includes; then
				CFLAGS="-I$dir/include $CFLAGS"
				CXXFLAGS="-I$dir/include $CXXFLAGS"
			fi
			break
		fi
		if test -f $dir/lib/libfltk.a; then
			fltklibs=$dir/lib
			if test x$dir != x/usr -a x$dir/include != x$x_includes; then
				CFLAGS="-I$dir/include $CFLAGS"
				CXXFLAGS="-I$dir/include $CXXFLAGS"
			fi
			break
		fi
	done
fi

if test x$fltklibs = x; then
	AC_MSG_RESULT(no)
else
	AC_MSG_RESULT($fltklibs/libfltk.a)
fi

if test x$fltklibs != x -a x$ac_cv_prog_CXX != x; then
	INSTALL_SETUP="install-setup"
	LIST_SETUP="f 0555 root sys ${libdir}/epm/setup setup"
	SETUP="setup"
	SETUPLIBS="$fltklibs/libfltk.a -lX11"
else
	INSTALL_SETUP=""
	LIST_SETUP=""
	SETUP=""
	SETUPLIBS=""

	if test x$ac_cv_prog_CXX = x; then
		AC_MSG_WARN(Setup GUI requires C++ compiler.)
	else
		AC_MSG_WARN(Setup GUI requires static FLTK library.)
	fi
fi

AC_SUBST(INSTALL_SETUP)
AC_SUBST(LIST_SETUP)
AC_SUBST(SETUP)
AC_SUBST(SETUPLIBS)

dnl Update compiler options...
if test -n "$GCC"; then
	if test -z "$OPTIM"; then
		if test "$uname" = "HP-UX"; then
			OPTIM="-O1 -fforce-mem -fforce-addr -fcaller-saves"
		else
	        	OPTIM="-O2 -fforce-mem -fforce-addr -fcaller-saves"
		fi
	fi

	CXX="$CC"
	OPTIM="-Wall -Wno-return-type -fno-rtti $OPTIM"

	# See if GCC supports -fno-exceptions...
	echo "Testing if GCC supports -fno-exceptions..."
	OLDCFLAGS="$CFLAGS"
	CFLAGS="$CFLAGS -fno-exceptions"
	AC_TRY_COMPILE(,,
		OPTIM="$OPTIM -fno-exceptions"
		echo "Congratulations - your GCC supports exceptions; disabling them...",
		echo "GCC does not support exceptions...")
	CFLAGS="$OLDCFLAGS"

	# See if we are running Solaris; if so, try the -fpermissive option...
	if test "$uname" = SunOS; then
		echo "Testing if GCC supports -fpermissive..."

		OLDCFLAGS="$CFLAGS"
		CFLAGS="$CFLAGS -fpermissive"
		AC_TRY_COMPILE(,,
			OPTIM="$OPTIM -fpermissive"
			echo "Enabling -fpermissive to work around X header bugs...",
			echo "The -fpermissive option is not required by your GCC...")
		CFLAGS="$OLDCFLAGS"
	fi
else
	case $uname in
		AIX*)
			if test -z "$OPTIM"; then
				OPTIM="-O2"
			fi
			;;
		HP-UX*)
			if test -z "$OPTIM"; then
				OPTIM="+O2"
			fi
			OPTIM="$OPTIM +DAportable"
			;;
        	IRIX*)
			if test -z "$OPTIM"; then
        			OPTIM="-O2"
			fi
			if test $uversion -ge 62; then
				OPTIM="$OPTIM -n32 -mips3"
				OPTIM="$OPTIM -OPT:Olimit=3000"
				OPTIM="-fullwarn -woff 1209,1506,1692 $OPTIM"
			else
				OPTIM="-fullwarn $OPTIM"
			fi
			;;
		SunOS*)
			# Solaris
			if test -z "$OPTIM"; then
				OPTIM="-xO4"
			fi
			;;
		*)
			# Running some other operating system; inform the user they
			# should contribute the necessary options to
			# epm@easysw.com...
			echo "Building EPM with default compiler optimizations; contact"
			echo "epm@easysw.com with the uname and compiler options needed for"
			echo "your platform, or set the CFLAGS and CXXFLAGS environment"
			echo "variable before running configure."
			;;
	esac
fi

dnl Fix "prefix" variable if it hasn't been specified...
if test "$prefix" = "NONE"; then
	prefix="/usr"
fi

dnl Fix "mandir" variable if it hasn't been specified...
if test "$mandir" = "\${prefix}/man" -a "$prefix" = "/usr"; then
	case "$uname" in
        	FreeBSD* | NetBSD* | OpenBSD*)
        		# *BSD
        		mandir="/usr/share/man"
        		;;
        	IRIX*)
        		# SGI IRIX
        		mandir="/usr/share/catman/u_man"
        		;;
        	*)
        		# All others
        		mandir="/usr/man"
        		;;
	esac
fi

dnl More manpage stuff...
case "$uname" in
        FreeBSD* | NetBSD* | OpenBSD*)
        	# *BSD
        	CAT1EXT="0"
        	MAN1EXT="1"
        	CAT5EXT="0"
        	MAN5EXT="5"
        	;;
        *)
        	# All others
        	CAT1EXT="1"
        	MAN1EXT="1"
        	CAT5EXT="5"
        	MAN5EXT="5"
        	;;
esac

AC_SUBST(CAT1EXT)
AC_SUBST(MAN1EXT)
AC_SUBST(CAT5EXT)
AC_SUBST(MAN5EXT)

dnl EPM_SOFTWARE needs special attention for the default location...
if test x$softwaredir = x; then
	if test "$prefix" = "/usr"; then
		EPM_SOFTWARE="/etc/software"
	else
		EPM_SOFTWARE="$prefix/etc/software"
	fi
else
	EPM_SOFTWARE="$softwaredir"
fi

AC_DEFINE_UNQUOTED(EPM_SOFTWARE, "$EPM_SOFTWARE")
AC_SUBST(EPM_SOFTWARE)

AC_DEFINE_UNQUOTED(EPM_GZIP, "$GZIP")
AC_SUBST(EPM_GZIP)

AC_DEFINE_UNQUOTED(EPM_STRIP, "$STRIP")
AC_SUBST(EPM_STRIP)

AC_DEFINE_UNQUOTED(EPM_BINDIR, "$prefix/bin")
AC_SUBST(EPM_BINDIR)

AC_DEFINE_UNQUOTED(EPM_LIBDIR, "$prefix/lib/epm")
AC_SUBST(EPM_LIBDIR)

dnl Figure out the right option to use for the RPM build architecture...
if test "x$RPM" != "x"; then
	# It would be nice if Red Hat could make up its mind...
	rpmversion="`$RPM --version | awk '{print $3}' | awk -F. '{print $1 * 10000 + $2 * 100 + $3}'`"

	if test $rpmversion -lt 30000; then
		AC_DEFINE_UNQUOTED(EPM_RPMARCH, "--buildarch ")
	else
		if test $rpmversion -ge 40003; then
			AC_DEFINE_UNQUOTED(EPM_RPMARCH, "--target ")
		else
			AC_DEFINE_UNQUOTED(EPM_RPMARCH, "--target=")
		fi
	fi
fi

dnl Output the makefile, list file, and config header...
AC_OUTPUT(Makefile epm.list mkepmlist doc/Makefile)

chmod +x mkepmlist

dnl
dnl End of "$Id: configure.in,v 1.44 2001/10/17 12:16:08 mike Exp $".
dnl
